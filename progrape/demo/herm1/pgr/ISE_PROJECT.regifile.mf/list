// gravity3.pgdl
// Calculate Potential, Acceleration and Jerk
// Author: Andreas Ernst, August 18, 2006

#define NFLO 26
#define NMAN 16
#define NFIX 57
#define NACC 64
#define NEXAD 31
#define NEXAD2 1
#define FSHIFT pow(2.0,(double)NEXAD)

/MEM xj[3] <= x[][]  : float(NFLO, NMAN);
/MEM vj[3] <= v[][]  : float(NFLO, NMAN);
/MEM mj    <= m[]    : float(NFLO, NMAN);
/REG xi[3] <= x[][]  : float(NFLO, NMAN);
/REG vi[3] <= v[][]  : float(NFLO, NMAN);
/REG rx    => p[]    : fix(NACC);
/REG sx[3] => a[][]  : fix(NACC);
/REG tx[3] => jk[][] : fix(NACC);

/SCALE rx    : (1.0/FSHIFT);
/SCALE sx[3] : (1.0/FSHIFT);
/SCALE tx[3] : (1.0/FSHIFT);

/NVMP 1;
/NPIPE 1;

// Acceleration, 1st part

pg_float_sub           (xj,xi,dx,              NFLO,NMAN, 4);     // (1)
pg_float_mult          (dx,dx,dx2,             NFLO,NMAN, 2);     // (2)
pg_float_unsigned_add  (dx2[0],dx2[1],x2y2,    NFLO,NMAN, 4);     // (3)
pg_float_unsigned_add  (dx2[2],x2y2,r2,        NFLO,NMAN, 4);     // (4)
pg_float_sqrt          (r2,r1,                 NFLO,NMAN, 3);     // (5)
pg_float_mult          (r2,r1,r3,              NFLO,NMAN, 2);     // (6)
pg_float_mult          (r3,r2,r5,              NFLO,NMAN, 2);     // (7)
pg_float_recipro       (r5,r5i,                NFLO,NMAN, 2);     // (8)
pg_float_mult          (r5i,mj,mr5i,           NFLO,NMAN, 2);     // (9)
pg_float_mult          (mr5i,r2,mf,            NFLO,NMAN, 2);     // (10)

// Potential

--pg_float_mult          (mf,r2,mp,              NFLO,NMAN, 2);      // (11)
--pg_float_expadd        (mp,ps,                 NEXAD,NFLO,NMAN,1); // (12)
--pg_float_fixaccum      (ps,rx,                 NFLO,NMAN, NFIX, NACC, 2);  // (13)
pg_bits_regifile(mf,rx,  NFLO);

// Acceleration, 2nd part

pg_float_expadd        (mf,fs,                 NEXAD,NFLO,NMAN,1);// (14)
pg_float_mult          (fs,dx,fx,              NFLO,NMAN, 2);     // (15)
pg_float_fixaccum      (fx,sx,                 NFLO,NMAN, NFIX, NACC, 2);  // (16)

// Jerk, 1st term

pg_float_sub           (vj,vi,dv,              NFLO,NMAN, 4);     // (17)
pg_float_mult          (fs,dv,jk1,             NFLO,NMAN, 2);     // (18)

// Jerk, 2nd term

pg_float_mult          (dx,dv,xv,              NFLO,NMAN, 2);      // (19)
pg_float_unsigned_add  (xv[0],xv[1],xv1,       NFLO,NMAN, 4);      // (20)
pg_float_unsigned_add  (xv[2],xv1,xv2,         NFLO,NMAN, 4);      // (21)
pg_float_expadd        (xv2,xv2x2,             NEXAD2,NFLO,NMAN,1);// (22)
pg_float_unsigned_add  (xv2x2,xv2,xv2x3,       NFLO,NMAN, 4);      // (23)
pg_float_mult          (mr5i,xv2x3,jk2a,       NFLO,NMAN, 2);      // (24)
pg_float_expadd        (jk2a,jk2s,             NEXAD,NFLO,NMAN,1); // (25)
pg_float_mult          (jk2s,dx,jk2,           NFLO,NMAN, 2);      // (26)

// Jerk, 1st - 2nd term

pg_float_sub           (jk1,jk2,jk0,           NFLO,NMAN, 4);      // (27)
pg_float_fixaccum      (jk0,tx,                NFLO,NMAN, NFIX, NACC, 2);  // (28)
